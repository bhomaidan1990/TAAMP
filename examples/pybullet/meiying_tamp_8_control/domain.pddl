(define (domain pick-and-place)
  (:requirements :strips :equality)
  (:predicates
    (Stackable ?o ?r)
    (Sink ?r)
    (Stove ?r)
    (Movable ?o)
    (Tool ?o)
    (Manipulanda ?o)
    (Graspable ?o)
    (Different ?o1 ?o2)
    (Floor ?r)

    (Pull ?o)

    (Pose ?o ?p)
    (ToolGoalPose ?o ?p)
    (Grasp ?o ?g)
    (Conf ?q)
    (Kin ?o ?p ?g ?q ?t)
    (PullConfigKin ?o ?p1 ?p2 ?p3 ?to ?g ?top ?q1 ?q2 ?t)
    (PullConfig ?o ?p1 ?p2 ?to ?g)
    ;(PullConfigTemp ?o ?p1 ?p2 ?to ?top1 ?top2 ?cg ?g)
    ;(PullKin ?to ?top1 ?top2 ?top ?g ?q ?t)
    ;(PullKinTemp ?top ?t)
    ;(PullGrasp ?o ?p1 ?p2 ?to ?cg)
    (FreeMotion ?q1 ?t ?q2)
    (HoldingMotion ?q1 ?t ?q2 ?o ?g)
    (Supported ?o ?p ?r)
    (Traj ?t)

    (TrajCollision ?t ?o2 ?p2)
    (CFreePosePose ?o ?p ?o2 ?p2)
    (CFreeApproachPose ?o ?p ?g ?o2 ?p2)
    (CFreeTrajPose ?t ?o2 ?p2)
    (CFreePullPose ?o ?p1 ?p2 ?to ?g ?o2 ?p22)
    (CFreePullTrajPose ?o ?to ?t ?o2 ?p2)

    (AtPose ?o ?p)
    (AtGrasp ?o ?g)
    (HandEmpty)
    (AtConf ?q)
    (CanMove)
    (Cleaned ?o)
    (Cooked ?o)

    (On ?o ?r)
    (Holding ?o)

    (UnsafePose ?o ?p)
    (UnsafeApproach ?o ?p ?g)
    (UnsafePull ?o ?p1 ?p2 ?to ?g)
    (UnsafeTraj ?t)
    (UnsafePullTraj ?o ?to ?t)

  )

  (:action move_free
    :parameters (?q1 ?q2 ?t)
    :precondition (and (FreeMotion ?q1 ?t ?q2)
                       (AtConf ?q1) (HandEmpty) (CanMove)
                       ;(not (UnsafeTraj ?t))
                  )
    :effect (and (AtConf ?q2)
                 (not (AtConf ?q1)) (not (CanMove)))
  )
  (:action move_holding
    :parameters (?q1 ?q2 ?o ?g ?t)
    :precondition (and (HoldingMotion ?q1 ?t ?q2 ?o ?g)
                       (AtConf ?q1) (AtGrasp ?o ?g) (CanMove)
                       ;(not (UnsafeTraj ?t))
                  )
    :effect (and (AtConf ?q2)
                 (not (AtConf ?q1)) (not (CanMove)))
  )

  (:action pick
    :parameters (?o ?p ?g ?q ?t)
    :precondition (and (Kin ?o ?p ?g ?q ?t)
                       (AtPose ?o ?p) (HandEmpty) (AtConf ?q) (Movable ?o)
                       (not (UnsafeApproach ?o ?p ?g))
                       (not (UnsafeTraj ?t))
                  )
    :effect (and (AtGrasp ?o ?g) (CanMove)
                 (not (AtPose ?o ?p)) (not (HandEmpty)))
  )

  (:action place
    :parameters (?o ?p ?g ?q ?t)
    :precondition (and (Kin ?o ?p ?g ?q ?t)
                       (AtGrasp ?o ?g) (AtConf ?q)
                       (not (UnsafePose ?o ?p))
                       (not (UnsafeApproach ?o ?p ?g))
                       (not (UnsafeTraj ?t))
                  )
    :effect (and (AtPose ?o ?p) (HandEmpty) (CanMove)
                 (not (AtGrasp ?o ?g)))
  )

  (:action pull
    :parameters (?o ?p1 ?p2 ?p3 ?to ?g ?q1 ?q2 ?top ?t)
    :precondition (and (AtPose ?o ?p1)
                       ;(Movable ?o)
                       (Manipulanda ?o)
                       (ToolGoalPose ?o ?p2)
                       ;(PullConfigTemp ?o ?p1 ?p2 ?to ?top1 ?top2 ?cg ?g)
                       (PullConfigKin ?o ?p1 ?p2 ?p3 ?to ?g ?top ?q1 ?q2 ?t)
                       ;(PullKin ?to ?top1 ?top2 ?top ?g ?q ?t)
                       ;(PullKinTemp ?top ?t)
                       (AtGrasp ?to ?g)
                       (Tool ?to)
                       (AtConf ?q1)
                       (not (= ?o ?to))
                       (not (UnsafePose ?o ?p2))
                       (not (UnsafePose ?to ?top2))
                       (not (UnsafePose ?to ?top))
                       (not (UnsafePull ?o ?p1 ?p3 ?to ?g))
                       (not (UnsafePullTraj ?o ?to ?t))
                  )
    :effect (and (AtPose ?o ?p3) (not (AtPose ?o ?p1))
                 (AtPose ?to ?top)
                 (not (AtGrasp ?to ?g)) (HandEmpty) (CanMove)
                 (not (AtConf ?q1)) (AtConf ?q2)
                 (Pull ?to)
            )
  )

  (:action clean
    :parameters (?o ?r)
    :precondition (and (Stackable ?o ?r) (Sink ?r)
                       (On ?o ?r))
    :effect (Cleaned ?o)
  )
  (:action cook
    :parameters (?o ?r)
    :precondition (and (Stackable ?o ?r) (Stove ?r)
                       (On ?o ?r) (Cleaned ?o))
    :effect (and (Cooked ?o)
                 (not (Cleaned ?o)))
  )

  (:derived (On ?o ?r)
    (exists (?p) (and (Supported ?o ?p ?r)
                      (AtPose ?o ?p)))
  )
  (:derived (Holding ?o)
    (exists (?g) (and (Grasp ?o ?g)
                      (AtGrasp ?o ?g)))
  )

  (:derived (UnsafePose ?o ?p)
    (exists (?o2 ?p2) (and (Pose ?o ?p) (Pose ?o2 ?p2) (not (= ?o ?o2))
                           (not (CFreePosePose ?o ?p ?o2 ?p2))
                           (AtPose ?o2 ?p2)))
  )
  (:derived (UnsafeApproach ?o ?p ?g)
    (exists (?o2 ?p2) (and (Pose ?o ?p) (Grasp ?o ?g) (Pose ?o2 ?p2) (not (= ?o ?o2))
                           (not (CFreeApproachPose ?o ?p ?g ?o2 ?p2))
                           (AtPose ?o2 ?p2)))
  )
  (:derived (UnsafePull ?o ?p1 ?p2 ?to ?g)
    (exists (?o2 ?p22) (and (Pose ?o ?p1) (Pose ?o ?p2)
                            ;(PullConfig ?o ?p1 ?p2 ?to ?top1 ?top2 ?cg ?g)
                            ;(PullConfig ?o ?p1 ?p2 ?to ?g ?cg)
                            (PullConfig ?o ?p1 ?p2 ?to ?g)
                            (Tool ?to)
                            (Pose ?o2 ?p22) (not (= ?o ?o2)) (not (= ?o ?to)) (not (= ?o2 ?to))
                            (not (CFreePullPose ?o ?p1 ?p2 ?to ?g ?o2 ?p22))
                            (AtPose ?o2 ?p22)
                       )
    )
  )
  (:derived (UnsafeTraj ?t)
    (exists (?o2 ?p2) (and (Traj ?t) (Pose ?o2 ?p2)
                           (not (CFreeTrajPose ?t ?o2 ?p2))
                           ; (TrajCollision ?t ?o2 ?p2)
                           (AtPose ?o2 ?p2)))
  )
  (:derived (UnsafePullTraj ?o ?to ?t)
    (exists (?o2 ?p2) (and (Traj ?t) (Pose ?o2 ?p2)
                           (Tool ?to) (Movable ?o)
                           (not (= ?o ?o2)) (not (= ?to ?o2)) (not (= ?o ?to))
                           (not (CFreePullTrajPose ?o ?to ?t ?o2 ?p2))
                           (AtPose ?o2 ?p2)
                      )
    )    
  )

)
